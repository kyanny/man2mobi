#!/usr/bin/env perl
use strict;
use warnings;
no warnings qw(uninitialized);

use File::Temp qw(tempfile);
use File::Spec::Functions qw(:ALL);
use Getopt::Long qw(HelpMessage :config auto_help);

my ($from, $to, $verbose);
GetOptions(
    "from=s" => \$from,
    "to=s" => \$to,
    "verbose+" => \$verbose,
);
my $name = shift || HelpMessage();

# main
sub main {
    if ($0 =~ /man2kindle$/) {
        print("run as `man2kindle'\n") if $verbose;
        man2kindle($name, $from, $to);
    }
    else {
        print("run as `man2mobi'\n") if $verbose;
        man2mobi($name);
    }
}

# man2mobi
sub man2mobi {
    my ($name) = shift;
    my @delete = ();

    # get man file's absolute path
    my $cmd = "man -W $name";
    _print($cmd) if $verbose;
    my $filename_man = `$cmd`;
    chomp $filename_man;

    # if man file is gzipped, extract it and save to temporary file
    if ($filename_man =~ /\.gz$/) {
        my ($tmp_fh_man, $tmp_filename_man) = tempfile;
        push @delete, $tmp_filename_man;
        $cmd = "gunzip -c $filename_man > $tmp_filename_man";
        _print($cmd) if $verbose;
        system($cmd);
        $filename_man = $tmp_filename_man;
    }

    # generate html file from man file by man2html
    my ($tmp_fh_html, $tmp_filename_html) = tempfile(SUFFIX => '.html');
    push @delete, $tmp_filename_html;
    $cmd = "man2html $filename_man > $tmp_filename_html";
    _print($cmd) if $verbose;
    `$cmd`;

    # generate mobi file by kindlegen
    $cmd = "kindlegen $tmp_filename_html";
    _print($cmd) if $verbose;
    `$cmd`;

    # copy generated mobi file (under temporary directory) to current directory
    (my $tmp_filename_mobi = $tmp_filename_html) =~ s/\.html/.mobi/;
    push @delete, $tmp_filename_mobi;
    my $filename_mobi = catfile(rel2abs(curdir), "$name.mobi");
    $cmd = "cp $tmp_filename_mobi $filename_mobi";
    _print($cmd) if $verbose;
    `$cmd`;

    # delete temporary files
    for (@delete) {
        _print("rm $_") if $verbose > 1;
        unlink($_);
    }
}

sub man2kindle {
    my ($name, $from, $to) = @_;
    HelpMessage() unless $from and $to;

    # generate mobi file by man2mobi
    man2mobi($name);

    # send email via mutt
    my $cmd = "mutt -e 'set from=$from' -a $name.mobi $to < /dev/null";
    _print($cmd) if $verbose;
    system($cmd);
}

sub _print { print "\$ ", @_, "\n"; }

# run
main();

__END__

=encoding utf8

=head1 NAME

man2mobi - マニュアルページを .mobi 形式に変換する

=head1 SYNOPSIS

$ man2mobi name

  または

  man2kindle -f foo@example.com -t bar@example.com name

=end

=head1 DESCRIPTION

man2mobi は name として指定されたマニュアルページを .mobi 形式に変換する。変換には man2html(1) と Amazon Kindle's Publishing Program で配布されている KindleGen が必要である。 KindleGen のアーカイブに含まれている kindlegen をパスの通ったところへ配置しておくこと。なお、 KindleGen の利用には別途 KINDLEGEN SOFTWARE END USER LICENSE AGREEMENT への同意が必要である。

man2kindle は man2mobi へのシンボリックリンクである。 man2kindle は name として指定されたマニュアルページを .mobi 形式に変換し、そのファイルをメッセージに添付してコマンドラインオプションで指定されたメールアドレスに送信する。メールの送信には mutt(1) が必要である。

Kindle に直接メールで .mobi 形式のファイルを送信する場合、 @kindle.com または @free.kindle.com というドメインのメールアドレスが利用できる。これらのアドレスにメールを送信するには、Manage Your Kindle ページで送信元メールアドレスを許可する必要がある。詳細は Sending Personal Documents to Kindle ページを参照のこと。

=head1 AUTHOR

Kensuke Kaneko <kyanny at gmail.com>

=head1 SEE ALSO

=over

=item *

man2html(1)

=item *

mutt(1)

=item *

Amazon Kindle's Publishing Program
http://www.amazon.com/gp/feature.html?ie=UTF8&docId=1000234621

=item *

Sending Personal Documents to Kindle
http://www.amazon.com/gp/help/customer/display.html?nodeId=200505520&#email

=item *

Manage Your Kindle
http://www.amazon.com/manageyourkindle

=back

=head1 LICENSE

This library is free software; you can redistribute it and/or modify it under the same terms as Perl itself.
